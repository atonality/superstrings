group 'io.atonality'
version '0.1'

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.google.apis:google-api-services-androidpublisher:v2-rev23-1.21.0'
    compile 'com.squareup.okhttp3:okhttp:3.1.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// stackoverflow.com/questions/4871656/using-gradle-to-build-a-jar-with-dependencies
jar {
    archiveName 'superstrings.jar'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task bundleRelease(type: Zip) {
    from("${project.buildDir}/libs") {
        include '*.jar'
    }
    from('src/main/groovy/io/atonality/superstrings') {
        include 'Superstrings.groovy'
    }
    from('release-scripts') {
        include '**/*'
    }
    destinationDir file("${project.buildDir}")
    archiveName "superstrings-${project.version}.zip"
}
tasks.bundleRelease.dependsOn(assemble)